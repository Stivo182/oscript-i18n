# oscript-i18n

Библиотека для интернационализации решения на OneScript.

## Описание

### Пакеты ресурсов локализации

Библиотека построена вокруг пакетов ресурсов локализации. Каждый пакет может состоять из нескольких файлов, содержащих данные для локализации. 
Состав ресурсов можно описать через текстовый формат [.properties](https://ru.wikipedia.org/wiki/.properties), либо через собственный класс `.os`. 
Описание ресурсов происходит через пары ключ-значение для каждой локали. В качестве значений могут быть: переводы текстов, шаблоны, форматы, настройки и прочее.

#### Структура каталогов

Файлы ресурсов рекомендуется хранить отдельно от исходников, чтобы ресурсы, описанные через класс (.os) не подключались самостоятельно в ваше приложение. Структуру каталогов можно использовать следующую:

```
...
├── src
└── locales/
    ├── ru/
    ├── en/
    ├── es/
    └── ...
```

#### Нейминг файлов ресурсов

Именование файла пакета играет важную роль, т.к. по имени файла определяется его базовое имя и локаль. Допустимые шаблоны имени файла следующие:

- `<Базовое имя пакета>.<Расширение>` - ресурсы будут доступны для всех локалей.
- `<Базовое имя пакета>_<Код языка>.<Расширение>` - ресурсы будут доступны для указанного языка и для всех стран языка.
- `<Базовое имя пакета>_<Код языка>_<Код страны>.<Расширение>` - ресурсы будут доступны только для указанного языка и страны.

#### Вариант описания пакета ресурсов через файл .properties

Файлы с расширением **.properties** являются обычными текстовыми файлами, в которой хранятся пары ключ-значений на каждой строке, записанные в формате: `ключ = значение` или `ключ : значение`.

<sub>./locales/ru/РесурсыКонсольногоПриложения_ru.properties</sub>
``` properties
ОписаниеПриложения = Приложение запускает \
                     и останавливает сервис

# Имена команд
Имя.Старт = Старт
Имя.Стоп = Стоп

# Описания команд
Описание.Старт = Останавливает сервис
Описание.Стоп = Запускает сервис
```

<sub>./locales/en/РесурсыКонсольногоПриложения_en.properties</sub>
``` properties
ОписаниеПриложения = Application starts and \
                     stops the service

# Имена команд
Имя.Старт = Start
Имя.Стоп = Stop

# Описания команд
Описание.Старт = Stops the service
Описание.Стоп = Starts the service
```

#### Вариант описания пакета ресурсов через класс (.os)

<sub>./locales/ru/РесурсыКонсольногоПриложения_ru.os</sub>
``` bsl
Функция ПолучитьРесурсы() Экспорт

	Ресурсы = Новый Соответствие();
	Ресурсы.Вставить("ОписаниеПриложения", "Приложение запускает и останавливает сервис");

	// Имена команд
	Ресурсы.Вставить("Имя.Старт", "Старт");
	Ресурсы.Вставить("Имя.Стоп", "Стоп");

	// Описания команд
	Ресурсы.Вставить("Описание.Старт", "Останавливает сервис");
	Ресурсы.Вставить("Описание.Стоп", "Запускает сервис");

	Возврат Ресурсы;

КонецФункции
```

<sub>./locales/en/РесурсыКонсольногоПриложения_en.os</sub>
``` bsl
Функция ПолучитьРесурсы() Экспорт

	Ресурсы = Новый Соответствие();
	Ресурсы.Вставить("ОписаниеПриложения", "Application starts and stops the service");

	// Имена команд
	Ресурсы.Вставить("Имя.Старт", "Start");
	Ресурсы.Вставить("Имя.Стоп", "Stop");

	// Описания команд
	Ресурсы.Вставить("Описание.Старт", "Stops the service");
	Ресурсы.Вставить("Описание.Стоп", "Starts the service");

	Возврат Ресурсы;

КонецФункции
```

#### Поиск файлов ресурсов пакета

Поиск файлов ресурсов происходит в определенном порядке:

Условия:
- Базовое имя пакета: `МоиРесурсы`
- Текущая локаль: `ru_RU`</br>
- Локаль по умолчанию: `en_UK`
- Имеющиеся файлы ресурсов: МоиРесурсы_en.os, МоиРесурсы.properties

Сначала загрузчик попытается найти файлы в следующем порядке:
- МоиРесурсы_ru_RU.os
- МоиРесурсы_ru_RU.properties
- МоиРесурсы_ru.os
- МоиРесурсы_ru.properties
- МоиРесурсы.os
- МоиРесурсы.properties

На этом этапе загрузчик найдет файл МоиРесурсы.properties, который будет пропущен, т.к. это базовый пакет. 
Далее загрузчик попытается найти следующие файлы:
 
- МоиРесурсы_en_UK.os
- МоиРесурсы_en_UK.properties
- МоиРесурсы_en.os
- МоиРесурсы_en.properties
- МоиРесурсы.os
- МоиРесурсы.properties

На этом этапе найдутся файлы МоиРесурсы_en.os и МоиРесурсы.properties, на основании которых будет собран пакет.

### Глобальные локали

Глобальные локали необходимы для определения текущей локали и локали по умолчанию. Взаимодействие с ними происходит через статичный класс библиотеки `РегиональныеНастройки`. 

#### Текущая локаль

Значение по умолчанию: `ru_RU`</br>
Используется для определения пакета ресурсов.

``` bsl
// Получение
ТекущаяЛокаль = РегиональныеНастройки.ТекущаяЛокаль();

// Установка
РегиональныеНастройки.УстановитьЛокаль("es_ES");
```

#### Локаль по умолчанию

Значение по умолчанию: `en_UK`</br>
Используется для определения пакета ресурсов, когда по текущей локали не найден пакет.

``` bsl
// Получение
ЛокальПоУмолчанию = РегиональныеНастройки.ЛокальПоУмолчанию();

// Установка
РегиональныеНастройки.УстановитьЛокальПоУмолчанию("es_ES");
```

## Использование

#### Подключение пакетов ресурсов

Чтобы подключить пакеты ресурсов, необходимо в точке инициализации вашего приложения добавить конструкцию добавления каталога к ресурсам:

<sub>./src/main.os</sub>
``` bsl
ПутьКРесурсам = Новый Файл(ОбъединитьПути(ТекущийСценарий().Каталог, "../locales")).ПолноеИмя;

МенеджерРесурсовЛокализации.ДобавитьКаталог(ПутьКРесурсам);
```

#### Локализация

##### Получение пакета

``` bsl
// Один пакет
Пакет = МенеджерРесурсовЛокализации.ПолучитьПакет("РесурсыКонсольногоПриложения");

// Либо группа пакетов
Пакет = МенеджерРесурсовЛокализации.ПолучитьПакет("РесурсыКонсольногоПриложения, ОбщиеРесурсы");
```

##### Получение конкретного ресурса

``` bsl
ОписаниеПриложения = Пакет.Получить("ОписаниеПриложения");
```

##### Заполнение шаблона

``` bsl
Шаблон = "{
|  'ОписаниеПриложения': '{t(ОписаниеПриложения)}',
|  'Команды': {
|    'Старт': {
|      'Имя': '{t(Имя.Старт)}',
|      'Описание': '{t(Описание.Старт)}'
|    },
|    'Стоп': {
|      'Имя': '{t(Имя.Стоп)}',
|      'Описание': '{t(Описание.Стоп)}'
|    }
|  }
|}";

Пакет.ЗаполнитьШаблон(Шаблон);
```
