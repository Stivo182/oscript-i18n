Перем Пакеты; // Массив из Объект.ПакетРесурсовЛокализации

// Добавляет пакет в группу
//
// Параметры:
//   Пакет - Объкт.ПакетРесурсовЛокализации
Процедура Добавить(Пакет) Экспорт
	Если Пакеты.Найти(Пакет) = Неопределено Тогда
		Пакеты.Добавить(Пакет);
	КонецЕсли;
КонецПроцедуры

// Проверяет наличие ресурса по ключу
//
// Параметры:
//   Ключ - Строка - Ключ ресурса
//
// Возвращаемое значение:
//   Булево
Функция СодержитКлюч(Ключ) Экспорт

	Для Каждого Пакет Из Пакеты Цикл
		Если Пакет.СодержитКлюч(Ключ) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

// Получает значение ресурса по ключу
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   Ключ - Строка - Ключ ресурса
//
// Возвращаемое значение:
//   Проивзольный
Функция Получить(Ключ) Экспорт

	Для Каждого Пакет Из Пакеты Цикл
		Значение = Пакет.Получить(Ключ);
		Если Не Значение = Неопределено Тогда
			Возврат Значение;
		КонецЕсли;
	КонецЦикла;

КонецФункции

// Заполняет шаблон ресурсами из пакета заменяя конструкцию {t(ИмяРесурса)} на значение ресурса
//
// Параметры:
//   Шаблон - Строка - Текст шаблона
Процедура ЗаполнитьШаблон(Шаблон) Экспорт
	ЛокализацияПриложения.ЗаполнитьШаблон(Шаблон, ЭтотОбъект);
КонецПроцедуры

Процедура ПриСозданииОбъекта()
	Пакеты = Новый Массив();
КонецПроцедуры