#Область ОписаниеПеременных

Перем Пакеты; // Массив из Объект.ПакетРесурсовЛокализации

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Добавляет пакет в группу
//
// Параметры:
//   Пакет - Объкт.ПакетРесурсовЛокализации - Пакет ресурсов
Процедура ДобавитьПакет(Пакет) Экспорт
	Если Пакеты.Найти(Пакет) = Неопределено Тогда
		Пакеты.Добавить(Пакет);
	КонецЕсли;
КонецПроцедуры

// Проверяет наличие ресурса по ключу
//
// Параметры:
//   Ключ - Строка - Имя ресурса
//
// Возвращаемое значение:
//   Булево
Функция СодержитКлюч(Ключ) Экспорт

	Для Каждого Пакет Из Пакеты Цикл
		Если Пакет.СодержитКлюч(Ключ) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

// Получает значение ресурса по ключу
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   Ключ - Строка - Имя ресурса
//
// Возвращаемое значение:
//   Проивзольный
Функция ПолучитьРесурс(Ключ) Экспорт

	Для Каждого Пакет Из Пакеты Цикл
		Значение = Пакет.ПолучитьРесурс(Ключ);
		Если Не Значение = Неопределено Тогда
			Возврат Значение;
		КонецЕсли;
	КонецЦикла;

КонецФункции

// Получает строковое значение ресурса с подставлением параметров по принципу функции СтрШаблон
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   ИмяРесурса - Строка - Имя ресурса
//   ЗначениеПараметра1-ЗначениеПараметра9 - Произвольный - Параметры, содержащие произвольные значения, строковые
//                                                          представления которых должны быть подставлены в шаблон 
//
// Возвращаемое значение:
//   Строка, Неопределено
Функция ПолучитьСтроку(ИмяРесурса,
	ЗначениеПараметра1 = Неопределено, ЗначениеПараметра2 = Неопределено, ЗначениеПараметра3 = Неопределено,
	ЗначениеПараметра4 = Неопределено, ЗначениеПараметра5 = Неопределено, ЗначениеПараметра6 = Неопределено,
	ЗначениеПараметра7 = Неопределено, ЗначениеПараметра8 = Неопределено, ЗначениеПараметра9 = Неопределено) Экспорт
	
	Возврат Мультиязычность.ПолучитьСтрокуИзПакета(ИмяРесурса, ЭтотОбъект,
		ЗначениеПараметра1, ЗначениеПараметра2, ЗначениеПараметра3,
		ЗначениеПараметра4, ЗначениеПараметра5, ЗначениеПараметра6,
		ЗначениеПараметра7, ЗначениеПараметра8, ЗначениеПараметра9);

КонецФункции

// Заполняет шаблон ресурсами из пакета заменяя конструкцию {t(ИмяРесурса)} на значение ресурса
//
// Параметры:
//   Шаблон - Строка - Текст шаблона
Процедура ЗаполнитьШаблон(Шаблон) Экспорт
	Мультиязычность.ЗаполнитьШаблон(Шаблон, ЭтотОбъект);
КонецПроцедуры

// Устанавливает группу пакетов ресурсов по умолчанию
Процедура ИспользоватьПоУмолчанию() Экспорт
	Мультиязычность.УстановитьПакетРесурсовПоУмолчанию(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Группа пакетов ресурсов
Процедура ПриСозданииОбъекта()
	Пакеты = Новый Массив();
КонецПроцедуры

#КонецОбласти