#Область ОписаниеПеременных

Перем _БазовоеИмя; // Строка
Перем _КодЛокализации; // Строка
Перем _Ресурсы; // Соответствие
Перем _КэшКлючей; // Соответствие
Перем _Родитель; // Объект.ПакетРесурсовЛокализации

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Получает базовое имя
//
// Возвращаемое значение:
//   Строка
Функция БазовоеИмя() Экспорт
	Возврат _БазовоеИмя;
КонецФункции

// Получает код локализации
//
// Возвращаемое значение:
//   Строка
Функция Локаль() Экспорт
	Возврат _КодЛокализации;
КонецФункции

// Проверяет наличие ресурса по ключу
//
// Параметры:
//   Ключ - Строка - Ключ ресурса
//
// Возвращаемое значение:
//   Булево
Функция СодержитКлюч(Ключ) Экспорт

	Если _КэшКлючей[Ключ] = Истина Тогда
		Возврат Истина;
	ИначеЕсли Не _Родитель = Неопределено Тогда
		Возврат _Родитель.СодержитКлюч(Ключ);
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Получает значение ресурса по ключу
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   Ключ - Строка - Ключ ресурса
//
// Возвращаемое значение:
//   Проивзольный
Функция Получить(Ключ) Экспорт

	Если _КэшКлючей[Ключ] = Истина Тогда
		Возврат _Ресурсы[Ключ];
	ИначеЕсли Не _Родитель = Неопределено Тогда
		Возврат _Родитель.Получить(Ключ);
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Заполняет шаблон ресурсами из пакета заменяя параметр вида {t(ИмяРесурса)}
//
// Параметры:
//   Шаблон - Строка - Текст шаблона
Процедура ЗаполнитьШаблон(Шаблон) Экспорт
	ЛокализацияПриложения.ЗаполнитьШаблон(Шаблон, ЭтотОбъект);
КонецПроцедуры

// Устанавливает родителя пакета
//
// Параметры:
//   Родитель - Объект.ПакетРесурсовЛокализации
Процедура УстановитьРодителя(Родитель) Экспорт
	_Родитель = Родитель;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Пакет локализации
//
// Параметры:
//   БазовоеИмя - Строка - Базовое имя пакета
//   КодЛокализации - Строка - Код локализации (ru_RU, en_US, de, nl)
//   Ресурсы - Соответствие- Ресурсы
Процедура ПриСозданииОбъекта(БазовоеИмя, КодЛокализации, Ресурсы)

	_БазовоеИмя = БазовоеИмя;
	_КодЛокализации = КодЛокализации;
	_Ресурсы = Ресурсы;
	_КэшКлючей = Новый Соответствие();
	_Родитель = Неопределено;

	Для Каждого Ресурс Из Ресурсы Цикл
		_КэшКлючей.Вставить(Ресурс.Ключ, Истина);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти