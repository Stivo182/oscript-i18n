#Область ОписаниеПеременных

Перем _БазовоеИмя; // Строка
Перем _КодЛокализации; // Строка
Перем _Ресурсы; // Соответствие
Перем _КэшКлючей; // Соответствие
Перем _Родитель; // Объект.ПакетРесурсовЛокализации

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Получает базовое имя
//
// Возвращаемое значение:
//   Строка
Функция БазовоеИмя() Экспорт
	Возврат _БазовоеИмя;
КонецФункции

// Получает код локализации
//
// Возвращаемое значение:
//   Строка
Функция Локаль() Экспорт
	Возврат _КодЛокализации;
КонецФункции

// Проверяет наличие ресурса по ключу
//
// Параметры:
//   Ключ - Строка - Имя ресурса
//
// Возвращаемое значение:
//   Булево
Функция СодержитКлюч(Ключ) Экспорт

	Если _КэшКлючей[Ключ] = Истина Тогда
		Возврат Истина;
	ИначеЕсли Не _Родитель = Неопределено Тогда
		Возврат _Родитель.СодержитКлюч(Ключ);
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Получает значение ресурса по ключу
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   Ключ - Строка - Имя ресурса
//
// Возвращаемое значение:
//   Проивзольный
Функция ПолучитьРесурс(Ключ) Экспорт

	Если _КэшКлючей[Ключ] = Истина Тогда
		Возврат _Ресурсы[Ключ];
	ИначеЕсли Не _Родитель = Неопределено Тогда
		Возврат _Родитель.ПолучитьРесурс(Ключ);
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Получает строковое значение ресурса с подставлением параметров по принципу функции СтрШаблон
// Возвращает Неопределено в случае, если ресурс не найден
//
// Параметры:
//   ИмяРесурса - Строка - Имя ресурса
//   ЗначениеПараметра1-ЗначениеПараметра9 - Произвольный - Параметры, содержащие произвольные значения, строковые
//                                                          представления которых должны быть подставлены в шаблон
//
// Возвращаемое значение:
//   Строка, Неопределено
Функция ПолучитьСтроку(ИмяРесурса,
	ЗначениеПараметра1 = Неопределено, ЗначениеПараметра2 = Неопределено, ЗначениеПараметра3 = Неопределено,
	ЗначениеПараметра4 = Неопределено, ЗначениеПараметра5 = Неопределено, ЗначениеПараметра6 = Неопределено,
	ЗначениеПараметра7 = Неопределено, ЗначениеПараметра8 = Неопределено, ЗначениеПараметра9 = Неопределено) Экспорт

	Возврат Мультиязычность.ПолучитьСтрокуИзПакета(ИмяРесурса, ЭтотОбъект,
		ЗначениеПараметра1, ЗначениеПараметра2, ЗначениеПараметра3,
		ЗначениеПараметра4, ЗначениеПараметра5, ЗначениеПараметра6,
		ЗначениеПараметра7, ЗначениеПараметра8, ЗначениеПараметра9);

КонецФункции

// Заполняет шаблон ресурсами из пакета заменяя конструкцию {t(ИмяРесурса)} на значение ресурса
//
// Параметры:
//   Шаблон - Строка - Текст шаблона
Процедура ЗаполнитьШаблон(Шаблон) Экспорт
	Мультиязычность.ЗаполнитьШаблон(Шаблон, ЭтотОбъект);
КонецПроцедуры

// Устанавливает ресурсы пакета
//
// Параметры:
//   Ресурсы - Соответствие - Ресурсы
Процедура УстановитьРесурсы(Ресурсы) Экспорт
	_Ресурсы = Ресурсы;

	Для Каждого Ресурс Из Ресурсы Цикл
		_КэшКлючей.Вставить(Ресурс.Ключ, Истина);
	КонецЦикла;
КонецПроцедуры

// Устанавливает родителя пакета
//
// Параметры:
//   Родитель - Объект.ПакетРесурсовЛокализации - Пакет ресурсов
Процедура УстановитьРодителя(Родитель) Экспорт
	_Родитель = Родитель;
КонецПроцедуры

// Устанавливает пакет ресурсов по умолчанию
Процедура ИспользоватьПоУмолчанию() Экспорт
	Мультиязычность.УстановитьПакетРесурсовПоУмолчанию(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Пакет ресурсов
//
// Параметры:
//   БазовоеИмя - Строка - Базовое имя пакета
//   КодЛокализации - Строка - Код локализации (ru_RU, en_US, de, nl)
Процедура ПриСозданииОбъекта(БазовоеИмя, КодЛокализации)

	_БазовоеИмя = БазовоеИмя;
	_КодЛокализации = КодЛокализации;
	_Ресурсы = Новый Соответствие();
	_КэшКлючей = Новый Соответствие();
	_Родитель = Неопределено;

КонецПроцедуры

#КонецОбласти